apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 35
    defaultConfig {
        applicationId "co.epitre.aelf_lectures"
        minSdk 21
        targetSdkVersion 35
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }
        kotlinOptions {
            jvmTarget = '17'
        }
    }
    buildFeatures {
        buildConfig true
    }
    buildTypes {
        integration {
            // Release build on test device
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-integration.pro'
            signingConfig signingConfigs.debug
        }
        release {
            // Release build for the Play Store
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }
    lint {
        abortOnError false
        checkReleaseBuilds false
    }
    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }
    namespace 'co.epitre.aelf_lectures'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'

    // Material design
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.google.android.material:material:1.12.0'

    // HTTP layer
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okhttp3:okhttp-tls:4.12.0'
    implementation 'com.squareup.moshi:moshi:1.15.0'

    // Synchronization
    implementation "androidx.work:work-runtime:2.10.0"

    // Foundation
    implementation 'org.greenrobot:eventbus:3.3.1'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    // Sqlite
    implementation project(path: ':sqlite3')

    // ** AUTOMATED TEST **
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('downloadMarkJs', DownloadFile) {
    sourceUrl = 'https://raw.githubusercontent.com/julmot/mark.js/8.11.1/dist/mark.min.js'
    target = new File('./src/main/assets/js/mark.8.11.1.min.js')
}
preBuild.dependsOn downloadMarkJs

//
// Custom tasks
//

class DownloadFile extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}
